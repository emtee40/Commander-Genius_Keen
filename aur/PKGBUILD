# Maintainer: Gerhard Stein <gerstrong@steinzone.de>
# Contributor: Peter Lewis <plewis@aur.archlinux.org>

pkgname=commander-genius-git
_pkgname=Commander-Genius
pkgver=2.8.3.r1.g279fb3cd6
pkgrel=1
pkgdesc="A modern implementation of the classic Commander Keen game series"
arch=('i686' 'x86_64')
url="http://clonekeenplus.sourceforge.net"
license=('GPL')
depends=('sdl2' 'mesa' 'sdl2_image' 'sdl2_ttf' 'sdl2_mixer')
makedepends=('git' 'cmake' 'glu')
provides=(commander-genius)
conflicts=(commander-genius)
source=(git+https://gitlab.com/Dringgstein/Commander-Genius.git)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  mkdir -p "$srcdir/$_pkgname-build"
  
  cmake -S "$srcdir/$_pkgname" -B "$srcdir/$_pkgname-build" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DAPPDIR=/usr/bin \
    -DFULL_GAMES_SHAREDIR:PATH=/usr/share \
    -DGAMES_SHAREDIR:PATH=/usr/share \
    -DDOCDIR=/usr/share/doc/commandergenius \
    -DUSE_BOOST=NO

  make -C "$_pkgname-build"
}

package() {
  cd "$srcdir/$_pkgname-build"
  make DESTDIR="$pkgdir/" install
}
